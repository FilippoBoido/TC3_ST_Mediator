<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.16">
  <POU Name="FB_Conveyor2Sequence" Id="{45d404a9-0aa4-4087-bc95-6cf151ca6fdc}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Conveyor2Sequence IMPLEMENTS I_ConveyorSequence
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	nStep : UINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="M_ConveyorSequence" Id="{2b20757a-3d83-4663-9374-c985d3021392}">
      <Declaration><![CDATA[
METHOD M_ConveyorSequence : BOOL
VAR_INPUT
	in_rConveyor	: REFERENCE TO FB_Conveyor;
END_VAR
VAR
	stMessage : ST_Message;
	ePeerReturnCode : E_PeerReturnCode;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE nStep OF
	
	0 :
 
		stMessage := 
			F_Message(
				eFromPeer := E_PeersList.ePEER_CONVEYOR_1,
				eToPeer := E_PeersList.ePEER_CONVEYOR_2,
				eMessage := E_MessageList.eCONVEYOR_1_AT_TRANSFER_POINT
			);
			
		ePeerReturnCode := in_rConveyor.M_CompareMessageAndEmptyMailbox(stMessage);
		
		IF ePeerReturnCode = E_PeerReturnCode.eMATCH_FOUND THEN
			in_rConveyor.M_StartConveyor();
			nStep := 20;
		END_IF
		
	20 :
	
		IF in_rConveyor.out_fbSensorStop.GetTriggerSignal THEN
			in_rConveyor.M_StopConveyor();
			nStep := 30;
		END_IF
		
	30 :
	
		stMessage :=
			F_Message(
				eFromPeer := E_PeersList.ePEER_CONVEYOR_2,
				eToPeer := E_PeersList.ePEER_CONVEYOR_3,
				eMessage := E_MessageList.eCONVEYOR_2_AT_TRANSFER_POINT
			);
			
		in_rConveyor.M_SendMessage(stMessage);
 		nStep := 40;
	
	40 : 
	
		stMessage := 
			F_Message(
				eFromPeer := E_PeersList.ePEER_CONVEYOR_1,
				eToPeer := E_PeersList.ePEER_CONVEYOR_2,
				eMessage := E_MessageList.eCONVEYOR_1_PACKAGE_DELIVERED
			);
			
		ePeerReturnCode := in_rConveyor.M_CompareMessageAndEmptyMailbox(stMessage);
		
		IF ePeerReturnCode = E_PeerReturnCode.eMATCH_FOUND THEN
			in_rConveyor.M_StartConveyor();
			nStep := 50;
		END_IF
	
	50: 
			
		IF in_rConveyor.out_fbSensorMaterialDispatch.GetTriggerSignal THEN
			
			stMessage := 
				F_Message(
					eFromPeer := E_PeersList.ePEER_CONVEYOR_2,
					eToPeer := E_PeersList.ePEER_CONVEYOR_3,
					eMessage := E_MessageList.eCONVEYOR_2_PACKAGE_DELIVERED
				);
				
			in_rConveyor.M_StopConveyor();
			in_rConveyor.M_SendMessage(stMessage);
			nStep := 0;
			
		END_IF		
				
END_CASE]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Conveyor2Sequence">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_Conveyor2Sequence.M_ConveyorSequence">
      <LineId Id="11" Count="3" />
      <LineId Id="68" Count="11" />
      <LineId Id="18" Count="16" />
      <LineId Id="80" Count="0" />
      <LineId Id="35" Count="22" />
      <LineId Id="82" Count="0" />
      <LineId Id="58" Count="4" />
      <LineId Id="81" Count="0" />
      <LineId Id="63" Count="2" />
      <LineId Id="83" Count="0" />
      <LineId Id="66" Count="1" />
      <LineId Id="7" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>